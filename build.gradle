buildscript {
    ext {
        kotlinVersion = '1.2.70'
        springBootVersion = '2.0.5.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-noarg:${kotlinVersion}")
    }
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-noarg'
apply plugin: 'kotlin-spring'
apply plugin: 'kotlin-jpa'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'com.jachouni'
version = '0.0.1'
sourceCompatibility = 1.8
compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation('org.springframework.boot:spring-boot-starter-data-jpa')
    implementation('com.fasterxml.jackson.module:jackson-module-kotlin')
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
    implementation("org.jetbrains.kotlin:kotlin-reflect")
//    implementation("org.springframework.boot:spring-boot-devtools")
//    implementation("org.springframework.boot:spring-boot-starter-actuator")
    // against:  java.lang.NoClassDefFoundError: javax/xml/bind/JAXBException if java >= 9.0
    // https://stackoverflow.com/questions/43574426/how-to-resolve-java-lang-noclassdeffounderror-javax-xml-bind-jaxbexception-in-j
    implementation 'javax.xml.bind:jaxb-api:2.3.0'
    implementation 'org.ajbrown:name-machine:1.0.0'
    implementation 'org.modelmapper:modelmapper:2.3.0'
    runtimeOnly('com.h2database:h2')

    compile "com.fasterxml.jackson.core:jackson-databind:2.8.5"
    runtime "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.8.5"
    testImplementation('org.springframework.boot:spring-boot-starter-test')
}

// See: https://kotlinexpertise.com/hibernate-with-kotlin-spring-boot/
allOpen {
    annotation("javax.persistence.Entity")
    annotation("javax.persistence.MappedSuperclass")
    annotation("javax.persistence.Embeddable")
}

noArg {
    // Annotation to create default constructor for ModelMapper: https://stackoverflow.com/questions/39199426/better-way-to-map-kotlin-data-objects-to-data-objects/51640371#51640371
    annotation("com.jachouni.mitabackend.DtoAnnotation")
}